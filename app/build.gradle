apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.greenrobot.greendao'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def storefile = properties.getProperty('storeFile')
def storepassword = properties.getProperty('storePassword')
def keyalias = properties.getProperty('keyAlias')
def keypassword = properties.getProperty('keyPassword')


android {
    compileSdkVersion 27
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'me.ykrank.s1next'
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 41
        versionName '1.3.3'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(storefile)
            storePassword storepassword
            keyAlias keyalias
            keyPassword keypassword
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex-libraries', 'true'))
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/greendao'
    }
    lintOptions {
        abortOnError = Boolean.valueOf(System.getProperty('abort-on-error', 'true'))
        textReport true
        textOutput 'stdout'
        disable 'MissingTranslation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        debug {
            multiDexEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        alpha {
            applicationIdSuffix '.alpha'
            versionNameSuffix '-alpha'
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            matchingFallbacks = ['release', 'debug']
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "DB_NAME", '"s1.db"'
    }
    flavorDimensions "market"
    productFlavors {
        play{
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "play.google.com"
            versionNameSuffix '-play'
        }
        normal{
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "normal"
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    options.compilerArgs << '-Xlint:unchecked,deprecation'
    options.compilerArgs << "-Xmaxerrs" << "500"
}

greendao {
    schemaVersion 6
}

//http://stackoverflow.com/questions/28683327/how-to-check-if-gradle-dependency-has-new-version
dependencies {
    implementation  project(path: ':lib')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    playImplementation fileTree(include: ['*.jar'], dir: 'libs/flavors/play')
    normalImplementation fileTree(include: ['*.jar'], dir: 'libs/flavors/normal')

    playImplementation 'com.tencent.bugly:crashreport:2.6.6.1'
    normalImplementation 'com.tencent.bugly:crashreport_upgrade:1.3.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
//    kapt "com.android.databinding:compiler:$databinding_version"

    implementation 'com.google.dagger:dagger:2.15'
    kapt 'com.google.dagger:dagger-compiler:2.15'
    // https://github.com/google/dagger/issues/95
//    provided 'javax.annotation:javax.annotation-api:1.3'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    alphaImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation "com.android.support:design:$rootProject.supportVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportVersion"
    implementation "com.android.support:percent:$rootProject.supportVersion"
    implementation "com.android.support:preference-v14:$rootProject.supportVersion"
    implementation "com.android.support:transition:$rootProject.supportVersion"
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // https://github.com/google/guava/wiki/UsingProGuardWithGuava
    // https://github.com/google/guava/issues/2117
    // do not update
    //noinspection NewerVersionAvailable
    implementation 'com.google.guava:guava:19.0'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'com.google.j2objc:j2objc-annotations:1.3'
    implementation 'org.apache.commons:commons-lang3:3.7'

    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.4.0'

    implementation "com.github.VictorAlbertos.RxCache:runtime:1.8.3-2.x"
    implementation 'com.github.VictorAlbertos.Jolyglot:jackson:0.0.4'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.5"

    implementation 'nz.bradcampbell:paperparcel:2.0.5'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.5' // Optional
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.5'

    implementation 'com.github.bumptech.glide:glide:4.6.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.6.1'
    kapt 'com.github.bumptech.glide:compiler:4.6.1'

    implementation 'cn.dreamtobe.filedownloader:filedownloader-okhttp3-connection:1.1.0'
    implementation 'com.liulishuo.filedownloader:library:1.7.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation 'com.github.ykrank:androidautodispose:0.4.0'

    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.shizhefei:LargeImageView:1.0.9'
    implementation 'com.hannesdorfmann:adapterdelegates3:3.0.2-SNAPSHOT'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.nononsenseapps:filepicker:4.1.0'
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.bigkoo:quicksidebar:1.0.3'
    implementation 'com.orhanobut:logger:2.2.0'
}
repositories {
    mavenCentral()
}
