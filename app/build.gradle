apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.greenrobot.greendao'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def storefile = properties.getProperty('storeFile')
def storepassword = properties.getProperty('storePassword')
def keyalias = properties.getProperty('keyAlias')
def keypassword = properties.getProperty('keyPassword')


android {
    compileSdkVersion 27
	buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'me.ykrank.s1next'
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 52
        versionName '2.0.3'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(storefile)
            storePassword storepassword
            keyAlias keyalias
            keyPassword keypassword
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex-libraries', 'true'))
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/greendao'
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError = Boolean.valueOf(System.getProperty('abort-on-error', 'true'))
        textReport true
        textOutput 'stdout'
        disable 'MissingTranslation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        debug {
            multiDexEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        alpha {
            multiDexEnabled true
            applicationIdSuffix '.alpha'
            versionNameSuffix '-alpha'
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            matchingFallbacks = ['release', 'debug']
        }

        release {
            multiDexEnabled true
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "DB_NAME", '"s1.db"'
    }
    flavorDimensions "market"
    productFlavors {
        play{
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "play.google.com"
            versionNameSuffix '-play'
        }
        normal{
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "normal"
        }
    }
}

kapt {
    mapDiagnosticLocations = true
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    options.compilerArgs << '-Xlint:unchecked,deprecation'
    options.compilerArgs << "-Xmaxerrs" << "500"
}

greendao {
    schemaVersion 6
}

//http://stackoverflow.com/questions/28683327/how-to-check-if-gradle-dependency-has-new-version
dependencies {
    implementation project(path: ':lib')
    implementation project(path: ':lib_keyboard')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    playImplementation fileTree(include: ['*.jar'], dir: 'libs/flavors/play')
    normalImplementation fileTree(include: ['*.jar'], dir: 'libs/flavors/normal')

    implementation 'com.google.dagger:dagger:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    // https://github.com/google/dagger/issues/95
//    provided 'javax.annotation:javax.annotation-api:1.3'

    alphaImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation "com.android.support:transition:$rootProject.supportVersion"

//    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
//    compileOnly 'com.google.j2objc:j2objc-annotations:1.3'

    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.4.0'

    implementation "com.github.VictorAlbertos.RxCache:runtime:1.8.3-2.x"
    implementation 'com.github.VictorAlbertos.Jolyglot:jackson:0.0.4'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6"

    implementation 'nz.bradcampbell:paperparcel:2.0.6'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.6' // Optional
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.6'

    kapt 'com.github.bumptech.glide:compiler:4.7.1'

    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.bigkoo:quicksidebar:1.0.3'

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    implementation 'me.jessyan:progressmanager:1.5.0'

    implementation 'com.nononsenseapps:filepicker:4.1.0'

    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.2.3'
}
