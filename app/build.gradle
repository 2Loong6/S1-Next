apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.databinding'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "cl.monsoon.s1next"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 6
        versionName "0.4.2"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        // Do not abort if lint finds an error,
        // otherwise the Travis CI build will fail.
        abortOnError false
        textReport true
        textOutput 'stdout'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

dependencies {
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    // https://github.com/google/dagger/issues/95
    compile 'javax.annotation:javax.annotation-api:1.2'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.0.5'

    apt 'com.android.databinding:compiler:1.0-rc1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'

    ext.supportVersion = "22.2.1"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"

    compile 'com.google.guava:guava:18.0'
    // https://github.com/google/guava/wiki/UsingProGuardWithGuava
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.trello:rxlifecycle-components:0.1.0'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-SNAPSHOT'
    compile 'com.squareup.retrofit:converter-jackson:2.0.0-SNAPSHOT'

    compile 'com.bugsnag:bugsnag-android:3.2.6'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
}
